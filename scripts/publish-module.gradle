apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = Skeletor.description
                    url = project.vcsUrl
                    licenses {
                        license {
                            name = Skeletor.licenseName
                            url = Skeletor.licenseUrl
                        }
                    }
                    developers {
                        developer {
                            id = Developer.id
                            name = Developer.fullName
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/ocandroid/skeletor.git'
                        developerConnection = 'scm:git:ssh://github.com/ocandroid/skeletor.git'
                        url = 'https://github.com/ocandroid/skeletor/tree/main'
                    }
                }
            }
        }
    }
}

//signing {
//    useInMemoryPgpKeys(
//            rootProject.ext["signing.keyId"],
//            rootProject.ext["signing.key"],
//            rootProject.ext["signing.password"],
//    )
//    sign publishing.publications
//}
